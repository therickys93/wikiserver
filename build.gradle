apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
  repositories { jcenter() }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.9'
  }
}
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'info.solidsoft.pitest'

javadoc {
    options {
        links( 'http://docs.oracle.com/javase/8/docs/api/' )
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        charSet 'UTF-8'
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/reports/jacoco"
    }
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: "${buildDir}/classes/main", 
            exclude: ['it/therickys93/wikiserver/server/**',
            		  'it/therickys93/wikiserver/database/**',
                      'it/therickys93/wikiserver/wiki/WikipediaSearchCommand.class',
                      'it/therickys93/wikiserver/wiki/AddCommand.class',
                      'it/therickys93/wikiserver/wiki/SwitchOffCommand.class',
                      'it/therickys93/wikiserver/wiki/SwitchOnCommand.class',
                      'it/therickys93/wikiserver/wiki/RemoveCommand.class',
                      'it/therickys93/wikiserver/wiki/DBStatusCommand.class',
                      'it/therickys93/wikiserver/wiki/WeatherCommand.class'])
        })
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://maven.restlet.org" }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile 'org.restlet.jse:org.restlet:2.3.12'
    compile 'org.restlet.jse:org.restlet.ext.jackson:2.3.12'
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.github.therickys93:wikiapi:1.1.34'
    compile 'com.github.bartlomiej-gora:RPNLibrary:3.2.2'
    compile 'org.slf4j:slf4j-api:1.7.22'
    compile 'com.github.fedy2:yahoo-weather-java-api:2.0.2'
    compile 'org.postgresql:postgresql:42.2.2'
    testCompile 'junit:junit:4.12'
    testCompile 'com.github.stefanbirkner:system-rules:1.17.0'
}

pitest {
    targetClasses = [ "it.therickys93.wikiserver.*" ]
}

shadowJar {
    archiveName 'wikiserver.jar'
}

task stage() {
    dependsOn clean, shadowJar
}

tasks.stage.doLast() {
    delete 'build/classes'
    delete 'build/dependency-cache'
    delete 'build/tmp'
    delete fileTree(dir: 'build/libs', exclude: '*.jar')
}

shadowJar.mustRunAfter clean
